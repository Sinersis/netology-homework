# Домашнее задание по лекции «Организация сети»

---
- Папка: [hw-95](assets%2Fterraform%2Fhw-95)
- Основных конфигурации сети: [1-main.tf](assets%2Fterraform%2Fhw-95%2F1-main.tf)
- NAT-инстанс: [2-nat-instance.tf](assets%2Fterraform%2Fhw-95%2F2-nat-instance.tf)
- Public-инстанс: [3-public-instance.tf](assets%2Fterraform%2Fhw-95%2F3-public-instance.tf)
- Private-инстанс: [4-private-instance.tf](assets%2Fterraform%2Fhw-95%2F4-private-instance.tf)
- Route tables: [5-rt.tf](assets%2Fterraform%2Fhw-95%2F5-rt.tf)


Для запуска заполнить [personal.auto.tfvars.example](assets%2Fterraform%2Fhw-95%2Fpersonal.auto.tfvars.example) и 
переименовать в personal.auto.tfvars

Полный листинг:

```hcl
terraform {
  required_providers {
    yandex = {
      source = "yandex-cloud/yandex"
    }
  }
  required_version = ">= 0.13"
}

locals {
  ssh-key = file(var.ssh_key_path)
}

data "yandex_compute_image" "ubuntu" {
  family = var.image_family
}

provider "yandex" {
  token     = var.token
  cloud_id  = var.cloud_id
  folder_id = var.folder_id
  zone      = var.default_zone
}

resource "yandex_vpc_network" "netology-network" {
  name = "netology-network"
}

resource "yandex_vpc_subnet" "netology-network-public" {
  name = "netology-network-public"
  v4_cidr_blocks = ["192.168.10.0/24"]
  zone           = "ru-central1-a"
  network_id     = yandex_vpc_network.netology-network.id
}

resource "yandex_vpc_subnet" "netology-network-private" {
  name = "netology-network-private"
  v4_cidr_blocks = ["192.168.20.0/24"]
  zone           = "ru-central1-a"
  network_id     = yandex_vpc_network.netology-network.id
  route_table_id = yandex_vpc_route_table.netology-route-table.id
}

resource "yandex_compute_instance" "nat-instance-platform" {
   name        = "nat-instance-platform"
   platform_id = "standard-v1"

   resources {
      cores         = 2
      memory        = 2
      core_fraction = 5
   }

   boot_disk {
      initialize_params {
         image_id = "fd80mrhj8fl2oe87o4e1"
         size     = 30
         type     = "network-ssd"
      }
   }

   scheduling_policy {
      preemptible = true
   }

   network_interface {
      subnet_id = yandex_vpc_subnet.netology-network-public.id
      ip_address = "192.168.10.254"
      nat = true
   }

   metadata = {
      serial-port-enable = 1
      ssh-keys           = "ubuntu:${local.ssh-key}"
   }

   provisioner "remote-exec" {
      inline = ["ping -c 10 ya.ru"]

      connection {
         host        = self.network_interface[0].nat_ip_address
         type        = "ssh"
         user        = "ubuntu"
         private_key = file(var.ssh_private_key_path)
         agent       = true
      }
   }
}

resource "yandex_compute_instance" "public-platform" {
   name        = "public-platform"
   platform_id = "standard-v1"

   resources {
      cores         = 2
      memory        = 2
      core_fraction = 5
   }

   boot_disk {
      initialize_params {
         image_id = data.yandex_compute_image.ubuntu.image_id
         size     = 30
         type     = "network-ssd"
      }
   }

   scheduling_policy {
      preemptible = true
   }

   network_interface {
      subnet_id = yandex_vpc_subnet.netology-network-public.id
      nat = true
   }

   metadata = {
      serial-port-enable = 1
      ssh-keys           = "ubuntu:${local.ssh-key}"
   }

   provisioner "remote-exec" {
      inline = ["ping -c 10 ya.ru"]

      connection {
         host        = self.network_interface[0].nat_ip_address
         type        = "ssh"
         user        = "ubuntu"
         private_key = file(var.ssh_private_key_path)
         agent       = true
      }
   }
}

resource "yandex_compute_instance" "private-platform" {
   name        = "private-platform"
   platform_id = "standard-v1"

   resources {
      cores         = 2
      memory        = 2
      core_fraction = 5
   }

   boot_disk {
      initialize_params {
         image_id = data.yandex_compute_image.ubuntu.image_id
         size     = 30
         type     = "network-ssd"
      }
   }

   scheduling_policy {
      preemptible = true
   }

   network_interface {
      subnet_id = yandex_vpc_subnet.netology-network-private.id
   }

   metadata = {
      serial-port-enable = 1
      ssh-keys           = "ubuntu:${local.ssh-key}"
   }
}

resource "yandex_vpc_route_table" "netology-route-table" {
   network_id = yandex_vpc_network.netology-network.id
   static_route {
      destination_prefix = "0.0.0.0/0"
      next_hop_address = yandex_compute_instance.nat-instance-platform.network_interface[0].ip_address
   }
}
```
