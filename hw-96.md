# Домашнее задание по лекции «Вычислительные мощности. Балансировщики нагрузки»

---
- Папка: [hw-96](assets%2Fterraform%2Fhw-96)
- Основных конфигурации: [1-main.tf](assets%2Fterraform%2Fhw-96%2F1-main.tf)
- Bucket: [2-backet.tf](assets%2Fterraform%2Fhw-96%2F2-backet.tf)
- Instance Group: [3-instance-group.tf](assets%2Fterraform%2Fhw-96%2F3-instance-group.tf)
- Balancer: [4-balancer.tf](assets%2Fterraform%2Fhw-96%2F4-balancer.tf)
- meta-data: [meta-data.yml](assets%2Fterraform%2Fhw-96%2Fmeta-data.yml)[5-rt.tf](assets%2Fterraform%2Fhw-95%2F5-rt.tf)


Для запуска заполнить [personal.auto.tfvars.example](assets%2Fterraform%2Fhw-95%2Fpersonal.auto.tfvars.example) и 
переименовать в personal.auto.tfvars

Полный листинг:

```hcl
terraform {
  required_providers {
    yandex = {
      source = "yandex-cloud/yandex"
    }
  }
  required_version = ">= 0.13"
}

locals {
  ssh-key = file(var.ssh_key_path)
}

data "yandex_compute_image" "ubuntu" {
  family = var.image_family
}

provider "yandex" {
  token     = var.token
  cloud_id  = var.cloud_id
  folder_id = var.folder_id
  zone      = var.default_zone
}

resource "yandex_iam_service_account" "netology-sa" {
  name = "netology-sa"
}

resource "yandex_resourcemanager_folder_iam_member" "editor" {
  folder_id = var.folder_id
  role      = "editor"
  member    = "serviceAccount:${yandex_iam_service_account.netology-sa.id}"
}

resource "yandex_vpc_network" "netology-network" {
  name = "netology-network"
}

resource "yandex_vpc_subnet" "netology-network-public" {
  depends_on = [yandex_vpc_network.netology-network]
  name = "netology-network-public"
  v4_cidr_blocks = ["192.168.10.0/24"]
  zone           = "ru-central1-a"
  network_id     = yandex_vpc_network.netology-network.id
}

resource "yandex_storage_bucket" "image-bucket" {
  depends_on = [yandex_vpc_subnet.netology-network-public]
  bucket = "images-bucket-netology"
  access_key = var.access_key_bucket
  secret_key = var.secret_key_bucket

  anonymous_access_flags {
    read        = true
    list        = true
    config_read = true
  }
}

resource "yandex_storage_object" "image-object" {
  depends_on = [yandex_storage_bucket.image-bucket]
  access_key = var.access_key_bucket
  secret_key = var.secret_key_bucket
  bucket = yandex_storage_bucket.image-bucket.bucket
  key    = "image.png"
  source = var.path_image_source
}

resource "yandex_compute_instance_group" "netology-group" {
  depends_on = [yandex_storage_object.image-object]
  name                          = "netology-group"
  service_account_id            = yandex_iam_service_account.netology-sa.id
  folder_id = var.folder_id

  instance_template {
    platform_id = "standard-v1"

    resources {
      cores         = 2
      memory        = 2
      core_fraction = 5
    }

    boot_disk {
      mode = "READ_WRITE"
      initialize_params {
        image_id = data.yandex_compute_image.ubuntu.image_id
        size     = 30
        type     = "network-ssd"
      }
    }

    scheduling_policy {
      preemptible = true
    }

    network_interface {
      network_id = yandex_vpc_network.netology-network.id
      subnet_ids = [yandex_vpc_subnet.netology-network-public.id]
      nat = true
    }

    metadata = {
      serial-port-enable = 1
      ssh-keys           = "ubuntu:${local.ssh-key}"
      user-data          = file("${path.module}/meta-data.yml")
    }

    network_settings {
      type = "STANDARD"
    }
  }

  deploy_policy {
    max_unavailable = 2
    max_creating = 2
    max_expansion = 2
    max_deleting = 2
  }

  load_balancer {
    target_group_name        = "netology-group"
    target_group_description = "Целевая группа Network Load Balancer"
  }

  allocation_policy {
    zones = [var.default_zone]
  }

  scale_policy {
    fixed_scale {
      size = 3
    }
  }
}

resource "yandex_lb_network_load_balancer" "netology-balancer" {
  depends_on = [yandex_compute_instance_group.netology-group]
  name = "netology-balancer"

  listener {
    name = "netology-balancer-1-listener"
    port = 80
    external_address_spec {
      ip_version = "ipv4"
    }
  }

  attached_target_group {
    target_group_id = yandex_compute_instance_group.netology-group.load_balancer.0.target_group_id

    healthcheck {
      name = "http"
      http_options {
        port = 80
        path = "/index.html"
      }
    }
  }
}
```